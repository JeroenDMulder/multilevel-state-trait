---
title: "Mplus"
output: 
  html_document:
    toc:  true
    toc_float: true
---

---

## Setup

Below you can find syntax for specifying the state-trait models in the `MODEL` command of Mplus. For SEM models with and without stationarity constraints, the data must be specified in wide-format. For ML-SEM models, data must be specified in long-format.

---

## Common Unique Trait State model {.tabset .tabset-fade}

### SEM CUTS
```
TITLE:      The SEM CUTS model.
          
DATA:       FILE = LISS_LS_wide.dat;
	
VARIABLE:   NAMES = person house 
                    IDEA1 UITS1 TEVR1 BELA1 VERA1 
                    IDEA2 UITS2 TEVR2 BELA2 VERA2 
                    IDEA3 UITS3 TEVR3 BELA3 VERA3 
                    IDEA4 UITS4 TEVR4 BELA4 VERA4
                    IDEA5 UITS5 TEVR5 BELA5 VERA5; 
            MISSING = .;
            USEVARIABLES = IDEA1-VERA5;

ANALYSIS:   MODEL = NOCOV;

MODEL:      ! Common trait factor loadings 
            CT BY IDEA1-VERA5; 

            ! Common trait variance (default)
            CT;
            
            ! Unique traits factor loadings
            UT_IDEA BY IDEA1@1 IDEA2 IDEA3 IDEA4 IDEA5; 
            UT_UITS BY UITS1@1 UITS2 UITS3 UITS4 UITS5;  
            UT_TEVR BY TEVR1@1 TEVR2 TEVR3 TEVR4 TEVR5; 
            UT_BELA BY BELA1@1 BELA2 BELA3 BELA4 BELA5;
            UT_VERA BY VERA1@1 VERA2 VERA3 VERA4 VERA5;
	        
            ! Unique trait variances (default)
            UT_IDEA-UT_VERA;
            
            ! Common state factor loadings
            CS1 BY IDEA1-VERA1;
            CS2 BY IDEA2-VERA2;
            CS3 BY IDEA3-VERA3;
            CS4 BY IDEA4-VERA4;
            CS5 BY IDEA5-VERA5;

            ! Common state variance (default)
            CS1-CS5;
            
            ! Unique state variances (default)
            IDEA1-VERA5;
    
            ! Means
            [IDEA1-VERA5];

OUTPUT:     SAMPSTAT TECH1 ;
```
---

### Stationary SEM CUTS
```
TITLE:      The SEM CUTS model with stationarity constraints over time.
          
DATA:       FILE = LISS_LS_wide.dat;
	
VARIABLE:   NAMES = person house 
                    IDEA1 UITS1 TEVR1 BELA1 VERA1 
                    IDEA2 UITS2 TEVR2 BELA2 VERA2 
                    IDEA3 UITS3 TEVR3 BELA3 VERA3 
                    IDEA4 UITS4 TEVR4 BELA4 VERA4
                    IDEA5 UITS5 TEVR5 BELA5 VERA5; 
            MISSING = .;
            USEVARIABLES = IDEA1-VERA5;

ANALYSIS:   MODEL = NOCOV;

MODEL:      ! Common trait factor loadings (constrained)
            CT BY IDEA1@1 UITS1 TEVR1 BELA1 VERA1 (g1-g5); 
            CT BY IDEA2@1 UITS2 TEVR2 BELA2 VERA2 (g1-g5); 
            CT BY IDEA3@1 UITS3 TEVR3 BELA3 VERA3 (g1-g5);
            CT BY IDEA4@1 UITS4 TEVR4 BELA4 VERA4 (g1-g5);
            CT BY IDEA5@1 UITS5 TEVR5 BELA5 VERA5 (g1-g5);
            
            ! Common trait variance (default)
            CT;
            
            ! Unique trait factor loadings (constrained)
            UT_IDEA BY IDEA1@1 IDEA2@1 IDEA3@1 IDEA4@1 IDEA5@1; 
            UT_UITS BY UITS1@1 UITS2@1 UITS3@1 UITS4@1 UITS5@1;  
          	UT_TEVR BY TEVR1@1 TEVR2@1 TEVR3@1 TEVR4@1 TEVR5@1; 
          	UT_BELA BY BELA1@1 BELA2@1 BELA3@1 BELA4@1 BELA5@1;
          	UT_VERA BY VERA1@1 VERA2@1 VERA3@1 VERA4@1 VERA5@1;
          	
          	! Common state factor loadings (constrained) 
          	CS1 BY IDEA1 UITS1 TEVR1 BELA1 VERA1 (k1-k5);
            CS2 BY IDEA2 UITS2 TEVR2 BELA2 VERA2 (k1-k5);
            CS3 BY IDEA3 UITS3 TEVR3 BELA3 VERA3 (k1-k5);
            CS4 BY IDEA4 UITS4 TEVR4 BELA4 VERA4 (k1-k5);
            CS5 BY IDEA5 UITS5 TEVR5 BELA5 VERA5 (k1-k5);

            ! Common state variances (constrained)
            CS1-CS5 (vcs);
            
            ! Unique state variances (constrained) 
            IDEA1 UITS1 TEVR1 BELA1 VERA1 (vus1-vus5); 
            IDEA2 UITS2 TEVR2 BELA2 VERA2 (vus1-vus5);
            IDEA3 UITS3 TEVR3 BELA3 VERA3 (vus1-vus5);
            IDEA4 UITS4 TEVR4 BELA4 VERA4 (vus1-vus5);
            IDEA5 UITS5 TEVR5 BELA5 VERA5 (vus1-vus5);
    
            ! Means (constrained) 
            [IDEA1 UITS1 TEVR1 BELA1 VERA1] (m1-m5); 
            [IDEA2 UITS2 TEVR2 BELA2 VERA2] (m1-m5);
            [IDEA3 UITS3 TEVR3 BELA3 VERA3] (m1-m5);
            [IDEA4 UITS4 TEVR4 BELA4 VERA4] (m1-m5);
            [IDEA5 UITS5 TEVR5 BELA5 VERA5] (m1-m5);
            
OUTPUT:     SAMPSTAT TECH1;
```

---

### ML-SEM CUTS
```
TITLE:      The SEM CUTS model with stationarity constraints over time.

DATA:       FILE = LISS_LS_long.dat;
	
VARIABLE:   NAMES = id wave person house IDEA UITS TEVR BELA VERA;
            USEVARIABLES = IDEA-VERA;
            CLUSTER = id;
            MISSING = .;

ANALYSIS:   TYPE = TWOLEVEL;
            ESTIMATOR = ML;

MODEL:      %WITHIN%
            CS BY IDEA-VERA;
            
            %BETWEEN%
            CT BY IDEA-VERA;

OUTPUT:     TECH1;
```

---


## Correlated Uniqueness model {.tabset .tabset-fade}


### SEM CU

```
TITLE:      The SEM CU model.
          
DATA:       FILE = LISS_LS_wide.dat;
	
VARIABLE:   NAMES = person house 
                    IDEA1 UITS1 TEVR1 BELA1 VERA1 
                    IDEA2 UITS2 TEVR2 BELA2 VERA2 
                    IDEA3 UITS3 TEVR3 BELA3 VERA3 
                    IDEA4 UITS4 TEVR4 BELA4 VERA4
                    IDEA5 UITS5 TEVR5 BELA5 VERA5; 
            MISSING = .;
            USEVARIABLES = IDEA1-VERA5;

ANALYSIS:   MODEL = NOCOV;

MODEL:      ! Trait factor loadings
            T BY IDEA1-VERA5;
            
            ! Trait variance (default)
            T;
            
            ! State Residual factor loadings
            SR1 BY IDEA1-VERA1;
            SR2 BY IDEA2-VERA2;
            SR3 BY IDEA3-VERA3;
            SR4 BY IDEA4-VERA4;
            SR5 BY IDEA5-VERA5;
            
            ! State Residual variances (default)
            SR1-SR5;
            
            ! Correlated measurement errors between the same methods over time
            IDEA1 WITH IDEA2 IDEA3 IDEA4 IDEA5; ! Method 1
            IDEA2 WITH IDEA3 IDEA4 IDEA5;
            IDEA3 WITH IDEA4 IDEA5;
            IDEA4 WITH IDEA5;

            UITS1 WITH UITS2 UITS3 UITS4 UITS5;
            UITS2 WITH UITS3 UITS4 UITS5;
            UITS3 WITH UITS4 UITS5;
            UITS4 WITH UITS5;

            TEVR1 WITH TEVR2 TEVR3 TEVR4 TEVR5;
            TEVR2 WITH TEVR3 TEVR4 TEVR5;
            TEVR3 WITH TEVR4 TEVR5;
            TEVR4 WITH TEVR5;
    
            BELA1 WITH BELA2 BELA3 BELA4 BELA5;
            BELA2 WITH BELA3 BELA4 BELA5;
            BELA3 WITH BELA4 BELA5;
            BELA4 WITH BELA5;

            VERA1 WITH VERA2 VERA3 VERA4 VERA5;
            VERA2 WITH VERA3 VERA4 VERA5;
            VERA3 WITH VERA4 VERA5;
            VERA4 WITH VERA5;

OUTPUT:     SAMPSTAT  TECH1; 
```

---

### Stationary SEM CU

```
TITLE:      The SEM CU model with stationarity constraints over time.
          
DATA:       FILE = LISS_LS_wide.dat;
	
VARIABLE:   NAMES = person house 
                    IDEA1 UITS1 TEVR1 BELA1 VERA1 
                    IDEA2 UITS2 TEVR2 BELA2 VERA2 
                    IDEA3 UITS3 TEVR3 BELA3 VERA3 
                    IDEA4 UITS4 TEVR4 BELA4 VERA4
                    IDEA5 UITS5 TEVR5 BELA5 VERA5; 
            MISSING = .;
            USEVARIABLES = IDEA1-VERA5;

ANALYSIS:   MODEL = NOCOV;

MODEL:      ! Trait factor loadings (constrained)
            T BY IDEA1@1 UITS1 TEVR1 BELA1 VERA1 (g1-g5);
            T BY IDEA2@1 UITS2 TEVR2 BELA2 VERA2 (g1-g5);
            T BY IDEA3@1 UITS3 TEVR3 BELA3 VERA3 (g1-g5);
            T BY IDEA4@1 UITS4 TEVR4 BELA4 VERA4 (g1-g5);
            T BY IDEA5@1 UITS5 TEVR5 BELA5 VERA5 (g1-g5);
            
            ! Trait variance (default)
            T;
            
            ! State Residual factors (constrained)
            SR1 BY IDEA1-VERA1 (k1-k5);
            SR2 BY IDEA2-VERA2 (k1-k5);
            SR3 BY IDEA3-VERA3 (k1-k5);
            SR4 BY IDEA4-VERA4 (k1-k5);
            SR5 BY IDEA5-VERA5 (k1-k5);
            
            ! State Residual variances (constrained)
            SR1-SR5 (vcs);
            
            ! Measurement error variances (constrained)
            IDEA1 UITS1 TEVR1 BELA1 VERA1 (vus1-vus5);
            IDEA2 UITS2 TEVR2 BELA2 VERA2 (vus1-vus5);
            IDEA3 UITS3 TEVR3 BELA3 VERA3 (vus1-vus5);
            IDEA4 UITS4 TEVR4 BELA4 VERA4 (vus1-vus5);
            IDEA5 UITS5 TEVR5 BELA5 VERA5 (vus1-vus5);
            
            ! Correlated measurement errors between the same methods over time (constrained)
            IDEA1 WITH IDEA2 IDEA3 IDEA4 IDEA5 (cov1);
            IDEA2 WITH IDEA3 IDEA4 IDEA5 (cov1);
            IDEA3 WITH IDEA4 IDEA5 (cov1);
            IDEA4 WITH IDEA5 (cov1);
            
            UITS1 WITH UITS2 UITS3 UITS4 UITS5 (cov2);
            UITS2 WITH UITS3 UITS4 UITS5 (cov2);
            UITS3 WITH UITS4 UITS5 (cov2);
            UITS4 WITH UITS5 (cov2);
            
            TEVR1 WITH TEVR2 TEVR3 TEVR4 TEVR5 (cov3);
            TEVR2 WITH TEVR3 TEVR4 TEVR5 (cov3);
            TEVR3 WITH TEVR4 TEVR5 (cov3);
            TEVR4 WITH TEVR5 (cov3);
    
   	        BELA1 WITH BELA2 BELA3 BELA4 BELA5 (cov4);
   	        BELA2 WITH BELA3 BELA4 BELA5 (cov4);
            BELA3 WITH BELA4 BELA5 (cov4);
            BELA4 WITH BELA5 (cov4);
            
            VERA1 WITH VERA2 VERA3 VERA4 VERA5 (cov5);
            VERA2 WITH VERA3 VERA4 VERA5 (cov5);
            VERA3 WITH VERA4 VERA5 (cov5);
            VERA4 WITH VERA5 (cov5);
            
            ! Means (constrained)
            [IDEA1 UITS1 TEVR1 BELA1 VERA1] (m1-m5);
            [IDEA2 UITS2 TEVR2 BELA2 VERA2] (m1-m5);
            [IDEA3 UITS3 TEVR3 BELA3 VERA3] (m1-m5);
            [IDEA4 UITS4 TEVR4 BELA4 VERA4] (m1-m5);
            [IDEA5 UITS5 TEVR5 BELA5 VERA5] (m1-m5);

OUTPUT:     SAMPSTAT TECH1;
```

---


### ML-SEM CU

From an ML-SEM perspective, there is no statistical difference between the CU model and the CUTS model. Therefore, we specify the ML-SEM CU model in the same way as the ML-SEM CUTS model. 

---

## Indicator Trait model{.tabset .tabset-fade}


### SEM IT 


```
TITLE:      The SEM IT model.
          
DATA:       FILE = LISS_LS_wide.dat;
	
VARIABLE:   NAMES = person house 
                    IDEA1 UITS1 TEVR1 BELA1 VERA1 
                    IDEA2 UITS2 TEVR2 BELA2 VERA2 
                    IDEA3 UITS3 TEVR3 BELA3 VERA3 
                    IDEA4 UITS4 TEVR4 BELA4 VERA4
                    IDEA5 UITS5 TEVR5 BELA5 VERA5; 
            MISSING = .;
            USEVARIABLES = IDEA1-VERA5;

ANALYSIS:   MODEL = NOCOV;

MODEL:      ! Indicator-specific trait factor loadings
          	IT1 BY IDEA1@1 IDEA2 IDEA3 IDEA4 IDEA5;
          	IT2 BY UITS1@1 UITS2 UITS3 UITS4 UITS5;
          	IT3 BY TEVR1@1 TEVR2 TEVR3 TEVR4 TEVR5;
          	IT4 BY BELA1@1 BELA2 BELA3 BELA4 BELA5;
          	IT5 BY VERA1@1 VERA2 VERA3 VERA4 VERA5;
          	
          	! Correlations between indicator-specific trait factors
          	IT1 WITH IT2-IT5;
          	IT2 WITH IT3-IT5;
          	IT3 WITH IT4 IT5;
          	IT4 WITH IT5;
          	
          	! Common state factor loadings
          	CS1 BY IDEA1 UITS1 TEVR1 BELA1 VERA1;
          	CS2 BY IDEA2 UITS2 TEVR2 BELA2 VERA2;
          	CS3 BY IDEA3 UITS3 TEVR3 BELA3 VERA3;
          	CS4 BY IDEA4 UITS4 TEVR4 BELA4 VERA4;
            CS5 BY IDEA5 UITS5 TEVR5 BELA5 VERA5;
            
            ! Unique states variances (default)
            IDEA1-VERA5;

OUTPUT:     SAMPSTAT  TECH1;

```

---

### Stationary SEM IT

```
TITLE:      The SEM IT model with stationarity constraints over time.
          
DATA:       FILE = LISS_LS_wide.dat;
	
VARIABLE:   NAMES = person house 
                    IDEA1 UITS1 TEVR1 BELA1 VERA1 
                    IDEA2 UITS2 TEVR2 BELA2 VERA2 
                    IDEA3 UITS3 TEVR3 BELA3 VERA3 
                    IDEA4 UITS4 TEVR4 BELA4 VERA4
                    IDEA5 UITS5 TEVR5 BELA5 VERA5; 
            MISSING = .;
            USEVARIABLES = IDEA1-VERA5;

ANALYSIS:   MODEL = NOCOV;

MODEL:      ! Indicator-specific trait factor loadings
            IT1 BY IDEA1@1 IDEA2@1 IDEA3@1 IDEA4@1 IDEA5@1;
            IT2 BY UITS1@1 UITS2@1 UITS3@1 UITS4@1 UITS5@1;
            IT3 BY TEVR1@1 TEVR2@1 TEVR3@1 TEVR4@1 TEVR5@1;
            IT4 BY BELA1@1 BELA2@1 BELA3@1 BELA4@1 BELA5@1;
	          IT5 BY VERA1@1 VERA2@1 VERA3@1 VERA4@1 VERA5@1;
	
	          ! Correlations between indicator-specific trait factors
	          IT1 WITH IT2-IT5;
	          IT2 WITH IT3-IT5;
            IT3 WITH IT4 IT5;
	          IT4 WITH IT5;
	          
	          ! Common state factor loadings (constrained)
	          CS1 BY IDEA1 UITS1 TEVR1 BELA1 VERA1 (k1-k5);
	          CS2 BY IDEA2 UITS2 TEVR2 BELA2 VERA2 (k1-k5);
	          CS3 BY IDEA3 UITS3 TEVR3 BELA3 VERA3 (k1-k5);
	          CS4 BY IDEA4 UITS4 TEVR4 BELA4 VERA4 (k1-k5);
            CS5 BY IDEA5 UITS5 TEVR5 BELA5 VERA5 (k1-k5);
	
            ! Common states variances (constrained)
            CS1-CS5 (vcs);
            
            ! Unique states factor loadings (constrained)
            IDEA1 UITS1 TEVR1 BELA1 VERA1 (vus1-vus5); 
            IDEA2 UITS2 TEVR2 BELA2 VERA2 (vus1-vus5);
            IDEA3 UITS3 TEVR3 BELA3 VERA3 (vus1-vus5);
            IDEA4 UITS4 TEVR4 BELA4 VERA4 (vus1-vus5);
            IDEA5 UITS5 TEVR5 BELA5 VERA5 (vus1-vus5);
    
            ! Means (constrained) 
            [IDEA1 UITS1 TEVR1 BELA1 VERA1] (m1-m5); 
            [IDEA2 UITS2 TEVR2 BELA2 VERA2] (m1-m5);
            [IDEA3 UITS3 TEVR3 BELA3 VERA3] (m1-m5);
            [IDEA4 UITS4 TEVR4 BELA4 VERA4] (m1-m5);
            [IDEA5 UITS5 TEVR5 BELA5 VERA5] (m1-m5);

OUTPUT:     SAMPSTAT TECH1; 
```

---

### ML-SEM IT 

```
TITLE:      The ML-SEM IT model.

DATA:       FILE = LISS_LS_long.dat;
	
VARIABLE:   NAMES = id wave person house IDEA UITS TEVR BELA VERA;
            USEVARIABLES = IDEA-VERA;
            CLUSTER = id;
            MISSING = .;

ANALYSIS:   TYPE = TWOLEVEL;
            ESTIMATOR = ML;

MODEL:      %WITHIN%
            cs BY IDEA-VERA;
            
            %BETWEEN%
            IDEA-VERA; ! Indicator-specific trait factors
            
            ! Correlations between IT factors
          	IDEA WITH UITS-VERA;
          	UITS WITH TEVR-VERA;
          	TEVR WITH BELA VERA;
          	BELA WITH VERA;

OUTPUT:     SAMPSTAT TECH1;
```

---


## Reference Indicator model{.tabset .tabset-fade}

### SEM RI

```
TITLE:      The SEM RI model.
          
DATA:       FILE = LISS_LS_wide.dat;
	
VARIABLE:   NAMES = person house 
                    IDEA1 UITS1 TEVR1 BELA1 VERA1 
                    IDEA2 UITS2 TEVR2 BELA2 VERA2 
                    IDEA3 UITS3 TEVR3 BELA3 VERA3 
                    IDEA4 UITS4 TEVR4 BELA4 VERA4
                    IDEA5 UITS5 TEVR5 BELA5 VERA5; 
            MISSING = .;
            USEVARIABLES = IDEA1-VERA5;

ANALYSIS:   MODEL = NOCOV;

MODEL:      ! General factor factor loading
            G BY IDEA1-VERA5;
            
            ! Trait residual factor loading
            ! IDEA serves as the reference indicator
            TR_UITS BY UITS1 UITS2 UITS3 UITS4 UITS5;
            TR_TEVR BY TEVR1 TEVR2 TEVR3 TEVR4 TEVR5;
            TR_BELA BY BELA1 BELA2 BELA3 BELA4 BELA5;
            TR_VERA BY VERA1 VERA2 VERA3 VERA4 VERA5;

            ! Allow covariances between trait residual factors
            TR_UITS WITH TR_TEVR TR_BELA TR_VERA;
            TR_TEVR WITH TR_BELA TR_VERA;
            TR_BELA WITH TR_VERA;
	
	          ! Occasion factors
	          O1 BY IDEA1-VERA1;
	          O2 BY IDEA2-VERA2;
	          O3 BY IDEA3-VERA3;
	          O4 BY IDEA4-VERA4;
	          O5 BY IDEA5-VERA5; 

OUTPUT:     SAMPSTAT TECH1;
```

---

### Stationary SEM RI

```
TITLE:      The SEM RI model with stationary constraints over time.
          
DATA:       FILE = LISS_LS_wide.dat;
	
VARIABLE:   NAMES = person house 
                    IDEA1 UITS1 TEVR1 BELA1 VERA1 
                    IDEA2 UITS2 TEVR2 BELA2 VERA2 
                    IDEA3 UITS3 TEVR3 BELA3 VERA3 
                    IDEA4 UITS4 TEVR4 BELA4 VERA4
                    IDEA5 UITS5 TEVR5 BELA5 VERA5; 
            MISSING = .;
            USEVARIABLES = IDEA1-VERA5;

ANALYSIS:   MODEL = NOCOV;

MODEL:      ! General factor (constrained)
            G BY IDEA1@1 UITS1 TEVR1 BELA1 VERA1 (l1-l5);
            G BY IDEA2@1 UITS2 TEVR2 BELA2 VERA2 (l1-l5);
            G BY IDEA3@1 UITS3 TEVR3 BELA3 VERA3 (l1-l5);
            G BY IDEA4@1 UITS4 TEVR4 BELA4 VERA4 (l1-l5);
            G BY IDEA5@1 UITS5 TEVR5 BELA5 VERA5 (l1-l5);
            
            ! Trait residual factors (constrained)
            ! IDEA serves as the reference indicator
            TR_UITS BY UITS1@1 UITS2@1 UITS3@1 UITS4@1 UITS5@1;
            TR_TEVR BY TEVR1@1 TEVR2@1 TEVR3@1 TEVR4@1 TEVR5@1;
            TR_BELA BY BELA1@1 BELA2@1 BELA3@1 BELA4@1 BELA5@1;
            TR_VERA BY VERA1@1 VERA2@1 VERA3@1 VERA4@1 VERA5@1;
            
            ! Covariances between method factors
            TR_UITS WITH TR_TEVR TR_BELA TR_VERA;
            TR_TEVR WITH TR_BELA TR_VERA;
            TR_BELA WITH TR_VERA;
            
            ! Occasion factors (constrained)
            O1 BY IDEA1@1 UITS1 TEVR1 BELA1 VERA1 (k1-k5);
            O2 BY IDEA2@1 UITS2 TEVR2 BELA2 VERA2 (k1-k5);
            O3 BY IDEA3@1 UITS3 TEVR3 BELA3 VERA3 (k1-k5);
            O4 BY IDEA4@1 UITS4 TEVR4 BELA4 VERA4 (k1-k5);
            O5 BY IDEA5@1 UITS5 TEVR5 BELA5 VERA5 (k1-k5);
            
            ! Occasion factor variances (constrained)
            O1-O5 (omega);
            
            ! Measurement error variances (constrained)
            IDEA1 UITS1 TEVR1 BELA1 VERA1 (sigma1-sigma5);
            IDEA2 UITS2 TEVR2 BELA2 VERA2 (sigma1-sigma5);
            IDEA3 UITS3 TEVR3 BELA3 VERA3 (sigma1-sigma5);
            IDEA4 UITS4 TEVR4 BELA4 VERA4 (sigma1-sigma5);
            IDEA5 UITS5 TEVR5 BELA5 VERA5 (sigma1-sigma5);
            
            ! Means (constrained)
            [IDEA1 UITS1 TEVR1 BELA1 VERA1] (m1-m5);
            [IDEA2 UITS2 TEVR2 BELA2 VERA2] (m1-m5);
            [IDEA3 UITS3 TEVR3 BELA3 VERA3] (m1-m5);
            [IDEA4 UITS4 TEVR4 BELA4 VERA4] (m1-m5);
            [IDEA5 UITS5 TEVR5 BELA5 VERA5] (m1-m5);
            
OUTPUT:     SAMPSTAT TECH1;
```

---


### ML-SEM RI 

```
TITLE:      The ML-SEM RI model.

DATA:       FILE = LISS_LS_long.dat;
	
VARIABLE:   NAMES = id wave person house IDEA UITS TEVR BELA VERA;
            USEVARIABLES = IDEA-VERA;
            CLUSTER = id;
            MISSING = .;

ANALYSIS:   TYPE = TWOLEVEL;
            ESTIMATOR = ML;

MODEL:	    %WITHIN%
            O BY IDEA-VERA;
            
            %BETWEEN%
            G BY IDEA-VERA;
            
            IDEA@0; ! IDEA serves as reference indicator
            UITS-VERA; ! Include trait residual factors
            UITS WITH TEVR-VERA; ! Include covariance between method residual factors
            TEVR WITH BELA VERA;
            BELA WITH VERA;
	
OUTPUT:     SAMPSTAT TECH1;
```

---
